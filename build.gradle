plugins {
    id 'java'
    id 'java-library'
    id 'maven'
    id "com.jfrog.bintray" version "1.8.2"
}

group 'net.hearthsim'
version '1.0'

def artifactId = "java-deckstrings"
def website = "https://github.com/Hearthsim/java-deckstgrings"

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name artifactId
            url website
            inceptionYear "2018"
            scm {
                connection website
                developerConnection website
                url website
            }
        }
    }
}

def props = new Properties()
def f = project.file('bintray.properties')
if (f.exists()) {
    props.load(f.newDataInputStream())
}

bintray {
    user = props.getProperty("bintray.user")
    key = props.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "net.hearthsim"
        name = artifactId
        userOrg = "hearthsim"
        websiteUrl = website
        vcsUrl = website
        licenses = website
        publish = true
        version {
            name = project.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}